plugins {
    id 'java'
    id 'groovy'
    id 'com.google.protobuf' version '0.9.5'
    id 'application'
}

group='com.yuezm.project.connector'
version='1.0.0'

repositories {
    mavenCentral()
    mavenLocal()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

application {
    mainClass = 'com.yuezm.project.connector.Application'
    applicationDefaultJvmArgs = [
            '--enable-preview',
            '--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED',
            '--add-opens', 'java.base/java.nio=ALL-UNNAMED',
            '--add-opens', 'java.base/java.util=ALL-UNNAMED',
            '--add-exports', 'java.base/sun.nio.ch=ALL-UNNAMED',
            '--add-exports', 'java.base/sun.security.provider=ALL-UNNAMED',
            '--illegal-access=permit'
    ]
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs += ['--enable-preview']
}

compileGroovy {
    groovyOptions.encoding = 'UTF-8'
    groovyOptions.javaAnnotationProcessing = true
    options.compilerArgs += ['--enable-preview']
}

tasks.withType(JavaExec) {
    jvmArgs += "--add-opens=java.base/sun.nio.ch=ALL-UNNAMED"
}

dependencies {
    // Groovy
    implementation 'org.apache.groovy:groovy:5.0.0'
    implementation 'org.apache.groovy:groovy-sql:5.0.0'
    implementation 'org.apache.groovy:groovy-json:5.0.0'
    implementation 'org.codehaus.gpars:gpars:1.2.1'

    // 数据库驱动
    implementation 'org.postgresql:postgresql:42.5.4'
    implementation 'com.oracle.database.jdbc:ojdbc8:21.10.0.0'
    implementation 'mysql:mysql-connector-java:8.0.28'
    implementation 'dm.jdbc:dm-driver:8.1.2.141'

    // Protobuf
    implementation "com.google.protobuf:protobuf-java:3.24.4"

    // Aeron
    implementation "io.aeron:aeron-all:1.45.0"
    implementation "org.agrona:agrona:1.23.0"

    // 数据源连接池
    implementation 'com.zaxxer:HikariCP:6.3.0'

    // 注解
    implementation 'jakarta.annotation:jakarta.annotation-api:2.1.1'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    // 测试
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

// 在编译 Groovy 代码前生成 proto
tasks.named('compileGroovy') {
    dependsOn tasks.named('generateProto')
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.24.4"
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins { /* 不加 grpc */ }
        }
    }
}

// 打包可执行 jar
jar {
    manifest {
        attributes('Main-Class': 'com.yuezm.project.connector.Application')
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
